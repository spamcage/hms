<!DOCTYPE html>
<html dir="ltr" lang="zh-cmn-hans">

<!-- Mirrored from www.hmailserver.org/viewtopic.php?f=15&t=95&view=print  , Sun, 20 Mar 2022 03:20:33 GMT -->
<meta http-equiv="content-type" content="text/html;charset=UTF-8" />
<head>  <script async src="https://pagead2.googlesyndication.com/pagead/js/adsbygoogle.js?client=ca-pub-1885816497771161" crossorigin="anonymous"></script>
<meta charset="utf-8" />
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<meta name="robots" content="noindex" />

<title>hMailServer中文论坛 &bull; hMailServer批量添加用户的方法</title>

<link href="styles/prosilver/theme/print.css" rel="stylesheet">
<link href="styles/prosilver/theme/bidi.css" rel="stylesheet">
</head>
<body id="phpbb" class="ltr">
<div id="wrap" class="wrap">
	<a id="top" class="top-anchor" accesskey="t"></a>

	<div id="page-header">
		<h1>hMailServer中文论坛</h1>
		<p>hMailServer中文论坛于2014年上线,支持开源与共享.hMailServer是一个运行于Windows系统的,基于GPL授权的,免费的,开源的邮件系统。<br /><a href="index.html">https://www.hmailserver.org/</a></p>

		<h2>hMailServer批量添加用户的方法</h2>
		<p><a href="viewtopic4c87.html?f=15&amp;t=95">https://www.hmailserver.org/viewtopic.php?f=15&amp;t=95</a></p>
	</div>

	<div id="page-body" class="page-body">
		<div class="page-number">分页： <strong>1</strong> / <strong>1</strong></div>
					<div class="post">
				<h3>hMailServer批量添加用户的方法</h3>
				<div class="date">发表于 : <strong>2015年4月19日, 16:23</strong></div>
				<div class="author">由 <strong>Hsia</strong></div>
				<div class="content">用脚本来批量添加用户，操作简单。<br>
下载文件，要修改的地方如下：<br>
import.vbs 修改第44行，这是登陆hmailserver的密码。<br>
<br>
bulk.csv   这是用户的数据。格式：User,tjones,mypassword,jones.com,Tommy,Jones<br>
参数：user：指用户。依次是用户、用户名、密码、域名、姓名。姓和名是要隔开的。<br>
例如：user,abc,123456,xxx.com,zhang,san<br>
设置空间大小修改下面这个0 ，此时的0表示无限制。1024=1M
<div class="codebox"><p>代码: <a href="#" onclick="selectCode(this); return false;">全选</a></p><pre><code>obNewAccount.Maxsize = 0 'sets mailbox size, 0=unlimited</code></pre></div>
然后运行脚本。<br>
重要说明:下载文件后，不管你放在哪里，这两个文件必须放在同一目录。
<div class="inline-attachment">
			
		
		
				<dl class="file">
			<dt><span class="imageset icon_topic_attach"></span> <a class="postlink" href="download/filee935.php?id=143">批量添加用户.rar</a></dt>
						<dd>(1.69 KiB) 下载 1141 次</dd>
		</dl>
		
		
			</div>

<br>
<br>
----------------------------------------------------分割线------------------------------------------------------------------<br>
<br>
<br>
我把完整的import.vbs脚本贴出来：
<div class="codebox"><p>代码: <a href="#" onclick="selectCode(this); return false;">全选</a></p><pre><code>'#####the script#####
'# The EntryType can be one of two options, User or Alias. If the EntryType is User, then:
'#
'# EntryType = User
'# Field1 = Uername
'# Field2 = Password
'# Field3 = DqmainName
'#
'# If the EntryType is Alias, then:
'# EntryType = Alias
'# Field1 = AliasName
'# Field2 = ForwardingEmail
'# Field3 = DomainName
'#
'# To add USERS, for example, your CSV file should have the structured information (type, strUsername, strPassword, strDomain, strPersonFirstName, strPersonLastName), like below:
'# User,tjones,mypassword,jones.com,Tommy,Jones
'#
'# To add ALIAS, for example, your CSV file should have the structured information (type, strAlias, strAliasUsername, strDomain), like below:
'# Alias,tommy,tjones@jones.com,jones.com
'#
'# This would create a user names tjones@jones.com in the jones.com domain and an alias tommy@jones.com which will forward all e-mail to tjones@jones.com in the jones.com domain.
'####################

Option Explicit
Dim Elog
Dim obBaseApp
Dim objFSO
Dim objTextFile
Dim strNewAlias,i

Dim failed
Dim added
Dim domainsAdded

failed = 0
added = 0
domainsAdded = 0

'#####################################################################
'# Custom Variables regarding HMAIL admin password and file to be read
'#####################################################################
Dim hAdminpwd
Dim CSVFile
hAdminpwd = "888888"
CSVFile = "bulk.csv"

Set Elog = CreateObject("hMailServer.EventLog")
Set obBaseApp = CreateObject("hMailServer.Application") 

Call obBaseApp.Authenticate("Administrator",hAdminpwd) '*** N.B. 1. set your administrator password in this line 

Const ForReading = 1
Set objFSO = CreateObject("Scripting.FileSystemObject")
Set objTextFile = objFSO.OpenTextFile(CSVFile, ForReading) 'N.B. 2. Set your CSV path/filename in this line
On Error resume next
Do While objTextFile.AtEndOfStream &lt;&gt; True
    strNewAlias = split(objTextFile.Readline, ",")

    Select Case strNewAlias(0)
       Case "User"
          AddUser strNewAlias(1), strNewAlias(2), strNewAlias(3),strNewAlias(4),strNewAlias(5)
       Case "Alias"
          AddAlias strNewAlias(1), strNewAlias(2), strNewAlias(3)
    End Select
    If err.Number &lt;&gt; 0 Then    'error handling:
     Elog.Write("Failed add (probably duplicate)" &amp; Chr(34) &amp; vbTab &amp; Chr(34) &amp; strNewAlias(0) _
     &amp; "," &amp; strNewAlias(1) &amp; "," &amp; strNewAlias(2) &amp; "," &amp; strNewAlias(3))
     failed = failed + 1
     err.Clear
    Else
     added = added + 1
    End If
Loop
On Error goto 0
Elog.Write("Domains Added Sucessfully    = " &amp; domainsAdded)
Elog.Write("Accounts Added Sucessfully   = " &amp; added)
Elog.Write("Failed or Duplicate Accounts = " &amp; failed)
Wscript.Echo ("Domains Added Sucessfully    = " &amp; domainsAdded &amp; VbNewLine _
            &amp; "Accounts Added Sucessfully   = " &amp; added &amp; VbNewLine _
            &amp; "Failed or Duplicate Accounts  = " &amp; failed &amp; VbNewLine &amp; VbNewLine _
            &amp; "See hmailserver_events.log for list of duplicates (if any)")

'###################
'# Usefull functions
'###################
Sub AddAlias(strAlias,strEmailAddress,strDomain)

   AddDomain strDomain

   Dim obDomain 
   Dim obAliases 
   Dim obNewAlias

   Set obDomain = obBaseApp.Domains.ItemByName(strDomain) 
   Set obAliases = obDomain.Aliases
   Set obNewAlias = obAliases.Add() 
   
   obNewAlias.Name = strAlias &amp; "@" &amp; strDomain 'username
   obNewAlias.Value = strEmailAddress 'password
   obNewAlias.Active = 1 'activates user
   obNewAlias.Save() 'saves account
   
   Set obNewAlias = Nothing
   Set obAliases = Nothing
   Set obDomain = Nothing
   
End Sub

Sub AddUser(strUsername, strPassword, strDomain, strPersonFirstName, strPersonLastName)
   
   AddDomain strDomain
   
   Dim obDomain 
   Dim obAccounts 
   Dim obNewAccount

   Set obDomain = obBaseApp.Domains.ItemByName(strDomain) 
   Set obAccounts = obDomain.Accounts
   Set obNewAccount = obAccounts.Add() 
   
   obNewAccount.Address = strUsername &amp; "@" &amp; strDomain 'username
   obNewAccount.Password = strPassword 'password
   obNewAccount.PersonFirstName = strPersonFirstName
   obNewAccount.PersonLastName = strPersonLastName
   obNewAccount.Active = 1 'activates user
   obNewAccount.Maxsize = 0 'sets mailbox size, 0=unlimited
   obNewAccount.Save() 'saves account
  
   Set obNewAccount = Nothing
   Set obDomain = Nothing   
   Set obAccounts = Nothing
  
End Sub

Sub AddDomain(strDomain)
   Dim obNewDomain
   Set obNewDomain = obBaseApp.Domains.Add()
   obNewDomain.Name = strDomain
   obNewDomain.Active = True

   On Error resume next
   obNewDomain.Save()
   If err.Number = 0 Then
    domainsAdded = domainsAdded + 1
   End If 
   err.Clear
 
   obBaseApp.Domains.Refresh()
End Sub</code></pre></div>
bulk.csv 的格式是：User,tjones,mypassword,jones.com,Tommy,Jones<br>
参数：user：指用户。依次是用户、用户名、密码、域名、姓名。姓和名是要隔开的。</div>
			</div>
			<hr />
					<div class="post">
				<h3>Re: hMailServer批量添加用户的方法</h3>
				<div class="date">发表于 : <strong>2015年5月8日, 09:13</strong></div>
				<div class="author">由 <strong>jasonshaw</strong></div>
				<div class="content">这个很好，学习试试怎么弄</div>
			</div>
			<hr />
					<div class="post">
				<h3>Re: hMailServer批量添加用户的方法</h3>
				<div class="date">发表于 : <strong>2015年11月25日, 21:18</strong></div>
				<div class="author">由 <strong>errai</strong></div>
				<div class="content">按帖子的提示设置了csv文件和修改了import.vbs的hmail管理密码，导入过程顺利，提示已经导入，结果到hmail管理程序进去一看，啥都么有！！！<br>
<br>
LOGS里有如下几行：<br>
<br>
﻿5776	"2015-11-25 21:16:02.444"	"Domains Added Sucessfully    = 0"<br>
﻿5776	"2015-11-25 21:16:02.446"	"Accounts Added Sucessfully   = 88"<br>
﻿5776	"2015-11-25 21:16:02.447"	"Failed or Duplicate Accounts = 0"<br>
<br>
提示已经导入了88个账户，结果都么有，晕倒，也没错误日志，不知道从何查起了！</div>
			</div>
			<hr />
					<div class="post">
				<h3>Re: hMailServer批量添加用户的方法</h3>
				<div class="date">发表于 : <strong>2016年12月2日, 11:10</strong></div>
				<div class="author">由 <strong>guiltysong</strong></div>
				<div class="content">感谢，导入成功了！</div>
			</div>
			<hr />
					<div class="post">
				<h3>Re: hMailServer批量添加用户的方法</h3>
				<div class="date">发表于 : <strong>2017年6月29日, 13:26</strong></div>
				<div class="author">由 <strong>tutututuqd</strong></div>
				<div class="content">我也出现了同样的错误，添加成功，但是看不到新增的账号。这是什么原因呢？</div>
			</div>
			<hr />
					<div class="post">
				<h3>Re: hMailServer批量添加用户的方法</h3>
				<div class="date">发表于 : <strong>2017年8月24日, 14:15</strong></div>
				<div class="author">由 <strong>AFEI</strong></div>
				<div class="content">我也是提示导入成功，但是数据库里没对应的账号信息！</div>
			</div>
			<hr />
					<div class="post">
				<h3>Re: hMailServer批量添加用户的方法</h3>
				<div class="date">发表于 : <strong>2017年9月21日, 14:54</strong></div>
				<div class="author">由 <strong>jujb.cn</strong></div>
				<div class="content">直接在提供下载的bulk上制作用户名单；重启后即可见已成功导入。向为此付出劳动的人们，致敬！</div>
			</div>
			<hr />
					<div class="post">
				<h3>Re: hMailServer批量添加用户的方法</h3>
				<div class="date">发表于 : <strong>2018年5月4日, 16:44</strong></div>
				<div class="author">由 <strong>yonglemcse</strong></div>
				<div class="content">域名空白没有新建时，即可批量导入，如果已经新建了域名，导入会出现失败！</div>
			</div>
			<hr />
			</div>

	<div id="page-footer" class="page-footer">
		<div class="page-number">全部次数是<abbr title="Asia/Shanghai">UTC+08:00</abbr><br />分页： <strong>1</strong> / <strong>1</strong></div>
			<div class="copyright">
				<p>由 <a href="https://www.phpbb.com/">phpBB</a>&reg; Forum Software &copy; phpBB Limited 提供支持
				</p>
								<p>Copyright © 2014-2022 <a href="index.html">hMailServer中文论坛</a>
				</p>
							</div>
	</div>
</div>

</body>

<!-- Mirrored from www.hmailserver.org/viewtopic.php?f=15&t=95&view=print  , Sun, 20 Mar 2022 03:20:33 GMT -->
</html>
